use crate::token::Token;
use crate::ast::{Expr, Stmt};
use crate::operator::{UnaryOperator, BinaryOperator};
use crate::lexer::Location;
use crate::error::LexicalError;

grammar<'s>;

pub Program: Vec<Stmt<'s>> = {
    <stmts: Stmt*> => stmts,
}

Stmt: Stmt<'s> = {
    "PRINT" <values:Expr*> ";" => { Stmt::Print { values, newline: false, wait: false }},
    "PRINTL" <values:Expr*> ";" => { Stmt::Print { values, newline: true, wait: false }},
    <var:var> "=" <value:Expr> ";" => { Stmt::Assign { var, value } },
    <Stmt> ";" => <>,
}

#[inline]
ExprList: Vec<Expr<'s>> = {
    <elements: ExprWithComma+> => elements,
}

#[inline]
ExprWithComma: Expr<'s> = {
    <expr:Expr> ","? => {
        expr
    }
}

Expr: Expr<'s> = {
    <lhs:Expr> <op:AddOp> <rhs:Factor> => Expr::BinaryOp {
        lhs: Box::new(lhs),
        rhs: Box::new(rhs),
        op,
    },
    Factor,
}

AddOp: BinaryOperator = {
    "+" => BinaryOperator::Add,
    "-" => BinaryOperator::Sub,
}

Factor: Expr<'s> = {
    <lhs:Factor> <op:MulOp> <rhs:Term> => Expr::BinaryOp {
        lhs: Box::new(lhs),
        rhs: Box::new(rhs),
        op,
    },
    Term,
}

MulOp: BinaryOperator = {
    "*" => BinaryOperator::Mul,
    "/" => BinaryOperator::Div,
}

Term: Expr<'s> = {
    <value:number> => { Expr::Number(value) },
    <value:string> => { Expr::String(value) },
    <var:var> => { Expr::Variable(var) },
    "(" <Expr> ")",
}

extern {
    type Location = Location;
    type Error = LexicalError;

    enum Token<'s> {
        "만약" => Token::If,
        "PRINT" => Token::Print,
        "PRINTL" => Token::PrintLine,
        "PRINTW" => Token::PrintWait,
        "{" => Token::OpenBrace,
        "}" => Token::CloseBrace,
        "(" => Token::OpenParan,
        ")" => Token::CloseParan,
        "+" => Token::BinaryOp(BinaryOperator::Add),
        "-" => Token::BinaryOp(BinaryOperator::Sub),
        "*" => Token::BinaryOp(BinaryOperator::Mul),
        "/" => Token::BinaryOp(BinaryOperator::Div),
        "%" => Token::BinaryOp(BinaryOperator::Rem),
        "!" => Token::UnaryOp(UnaryOperator::Not),
        "=" => Token::Assign,
        ";" => Token::SemiColon,
        "," => Token::Comma,
        number => Token::IntLit(<u32>),
        string => Token::StrLit(<&'s str>),
        var => Token::Variable(<&'s str>),
    }
} 

